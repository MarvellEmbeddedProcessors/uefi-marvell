//Based on ArmPlatformPkg/Library/ArmPlatformLibNull/AArch64/ArmPlatformHelper.S
//
//  Copyright (c) 2012-2013, ARM Limited. All rights reserved.
//  Copyright (c) 2016, Marvell. All rights reserved.
//  Copyright (c) 2017, Linaro Limited. All rights reserved.
//
//  This program and the accompanying materials are licensed and made available
//  under the terms and conditions of the BSD License which accompanies this
//  distribution. The full text of the license may be found at
//  http://opensource.org/licenses/bsd-license.php
//
//  THE PROGRAM IS DISTRIBUTED UNDER THE BSD LICENSE ON AN "AS IS" BASIS,
//  WITHOUT WARRANTIES OR REPRESENTATIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED
//

#include <AsmMacroIoLib.h>
#include <Library/ArmLib.h>

#define CCU_MC_BASE                     0xF0001700
#define CCU_MC_RCR_OFFSET               0x0
#define CCU_MC_RCR_REMAP_EN             BIT0
#define CCU_MC_RCR_REMAP_SIZE(Size)     (((Size) - 1) ^ (SIZE_1MB - 1))

#define CCU_MC_RSBR_OFFSET              0x4
#define CCU_MC_RSBR_SOURCE_BASE(Base)   (((Base) >> 20) << 10)
#define CCU_MC_RTBR_OFFSET              0x8
#define CCU_MC_RTBR_TARGET_BASE(Base)   (((Base) >> 20) << 10)

ASM_FUNC(ArmPlatformPeiBootAction)
  .if   FixedPcdGet64 (PcdSystemMemoryBase) != 0
  .err  PcdSystemMemoryBase should be 0x0 on this platform!
  .endif

  .if   FixedPcdGet64 (PcdSystemMemorySize) > FixedPcdGet32 (PcdDramRemapTarget)
    //
    // Remap the DRAM region [DramRemapTarget, DramRemapTarget + DramRemapSize)
    // to the top of the DRAM space. This is mainly intended to free up 32-bit
    // addressable memory for MMIO and PCI windows.
    //
    MOV32 (r0, CCU_MC_BASE)
    MOV32 (r1, CCU_MC_RSBR_SOURCE_BASE (FixedPcdGet64 (PcdSystemMemorySize)))
    MOV32 (r2, CCU_MC_RTBR_TARGET_BASE (FixedPcdGet32 (PcdDramRemapTarget)))
    MOV32 (r3, CCU_MC_RCR_REMAP_SIZE (FixedPcdGet32 (PcdDramRemapSize)) | CCU_MC_RCR_REMAP_EN)
    str   r1, [r0, #CCU_MC_RSBR_OFFSET]
    str   r2, [r0, #CCU_MC_RTBR_OFFSET]
    str   r3, [r0, #CCU_MC_RCR_OFFSET]

    //
    // Use the low range for UEFI itself. The remaining memory will be mapped
    // and added to the GCD map later.
    //
    ADRL  (r0, mSystemMemoryEnd)
    MOV32 (r2, FixedPcdGet32 (PcdDramRemapTarget) - 1)
    mov   r3, #0
    strd  r2, r3, [r0]

  .endif

  bx    lr

//UINTN
//ArmPlatformGetCorePosition (
//  IN UINTN MpId
//  );
// With this function: CorePos = (ClusterId * 2) + CoreId
ASM_FUNC(ArmPlatformGetCorePosition)
  and   r1, r0, #ARM_CORE_MASK
  and   r0, r0, #ARM_CLUSTER_MASK
  add   r0, r1, r0, LSR #7
  bx    lr

//UINTN
//ArmPlatformGetPrimaryCoreMpId (
//  VOID
//  );
ASM_FUNC(ArmPlatformGetPrimaryCoreMpId)
  MOV32 (r0, FixedPcdGet32(PcdArmPrimaryCore))
  bx    lr

//UINTN
//ArmPlatformIsPrimaryCore (
//  IN UINTN MpId
//  );
ASM_FUNC(ArmPlatformIsPrimaryCore)
  MOV32 (r1, FixedPcdGet32(PcdArmPrimaryCoreMask))
  and   r0, r0, r1
  MOV32 (r1, FixedPcdGet32(PcdArmPrimaryCore))
  cmp   r0, r1
  moveq r0, #1
  movne r0, #0
  bx    lr
